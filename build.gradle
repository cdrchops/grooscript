plugins {
    id "com.moowork.node" version "1.0.1"
    id "com.moowork.gulp" version "1.0.1"
    id "com.bmuschko.nexus" version "2.3.1"
    id "org.asciidoctor.convert" version "1.5.3"
    id "net.saliman.cobertura" version "2.2.6"
    id "com.github.kt3k.coveralls" version "2.3.1"
    id "com.github.hierynomus.license" version "0.11.0"
    id 'maven-publish'
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply from: "$rootDir/gradle/node.gradle"
apply from: "$rootDir/gradle/release.gradle"

def compatibilityVersion = 1.6
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

version = '1.3.2'
group = 'org.grooscript.conversion'

ext {
    groovyVersion = '2.4.13'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.codehaus.groovy:groovy:${groovyVersion}"
    runtime 'org.apache.ivy:ivy:2.4.0'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
        exclude module: 'junit-dep'
    }
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile "org.codehaus.groovy:groovy-test:${groovyVersion}"
}

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

jar {
    manifest {
        attributes("Implementation-Title": rootProject.name, "Implementation-Version": version)
    }
    exclude 'META-INF/resources/**/test*.js'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task generateGrooscriptToolsFiles(type: JavaExec) {
    description 'Generate grooscript js tools files'

    // Set main property to name of Groovy script class.
    main = 'org.grooscript.GenerateGrooscripTools'

    // Set classpath for running the Groovy script.
    classpath = sourceSets.main.runtimeClasspath
}

compileJava.dependsOn gulp_compress
test.dependsOn generateGrooscriptToolsFiles, gulp_tests, gulp_test_require, licenseMain, licenseTest

asciidoctor {
    sourceDir = project.file 'doc'
    sources { include 'doc.adoc' }
    attributes 'grooscript-version': version,
        author: 'Jorge Franco',
        email: 'grooscript@gmail.com',
        'source-highlighter': 'prettify',
        toc: 'right',
        toclevels: 2,
        icons: 'font',
        'index-file': 'index.html'
}

license {
    header = file('LICENSE.txt')
    include "**/*.groovy"
    includes(["**/*.java", "**/grooscript.js"])
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'grooscript'
            version = version

            from components.java
        }
    }
}